## Summary

本文讲解了 Basic shadow mapping 算法，以及其产生的自遮挡问题，并展示使用 Vulkan 实现的例子以及部分实现细节。

## Shadow Mapping algorithm

<img src="1. Basic Shadow Mapping.assets/image-20211007153324955.png" alt="image-20211007153324955" style="zoom: 33%;" /> 光照条件下，场景中的物体会由于遮挡关系而形成阴影，被遮挡的物体处在阴影中，如左图所示。

在同一光线方向上，离光源近的点会遮挡住其后的点。因此，我们只需要根据点到光源的距离即可得到遮挡关系，对被遮挡的物体进行阴影处理即可。

Shadow mapping 算法用于判断哪些物体处在阴影中，即哪些物体被遮挡，是一种 2-pass 算法：

1. Light pass：以 light 作为虚拟相机，在 light view space 中绘制场景，得到该场景的 shadow map，即 depth buffer。因此 light pass 中，只需要计算出 depth buffer，不需要进行着色，即不需要 fragment shader 阶段。

2. Camera pass：场景的着色 pass。在 camera view space 中绘制场景，同时将当前顶点变换到 light view space，得到 shadow map 中当前像素对应的深度值，并与该顶点在 light view space 的深度进行比较。若 shadow map 中的深度值较小，证明存在距光源更近的顶点，即当前顶点被遮挡。对于被遮挡的顶点，进行阴影着色，比如颜色置为黑色。

   > 当前顶点：经过 camera view 变换的顶点为相机可见顶点，即距相机最近的顶点。转换到 light view space，即可得到当前顶点距光源的距离，即 light view space 下的深度值。
   >
   > shadow map 中的深度值： 该像素只保留了在 light view space 对应的所有顶点中距离光源最近的顶点深度值。
   >
   >  从 shadow map 当前顶点对应的像素中得到最近顶点的深度值，再与当前顶点与 light 的距离进行比较，判断当前顶点是不是距光源最近的顶点。如果不是，则当前顶点被遮挡。

### Self-occlusion shadow

Shadow Mapping 算法的其中一个问题是自遮挡，当 light pass 生成的 shadow map 中像素与 camera pass 插值得到的 light space 下的像素不匹配时就发生了自遮挡。造成这种不匹配的原因有两个：

- light clip space 与 camera clip space 的变换不同，导致光栅化得到的像素不同。

  对于同一三角形，分别在 light pass 和 camera pass 进行光栅化，如下图所示：

  <img src="1. Basic Shadow Mapping.assets/image-20211011113545291.png" alt="image-20211011113545291" style="zoom: 67%;" />

  上图是 light pass 与 camera pass 对同一个三角形光栅化后的结果。很明显，light pass 中三角形包含的像素较少，而 camera pass 中包含的像素较多。这样就导致 camera pass 插值得到的 shadow map 与 light pass 生成的 shadow map 不一致。如上图所示，camera pass 中四个蓝色像素对应了 light pass 中的一个蓝色像素，其中蓝色方块的数字表示该像素的深度，因此，四个像素中有两个深度为 0.9 的像素发生了自遮挡。

- 数值精度有限导致的舍入误差

  浮点运算会引入舍入误差，不同的浮点运算舍入误差不同。light pass 和 camera pass 中包含了不同的浮点运算，如不同的变换、不同的插值参数，导致得到的结果会有一个误差差异值，这样就造成了不匹配。

自遮挡的表现就是出现了很多小阴影区域，如图 [自遮挡示例](#self-occlusion image)，常见解决方案之一是给light pass 生成的 shadow map中的 depth 加上一个偏移量(bias)。偏移量过小，自遮挡消除效果不明显。偏移量过大，会出现阴影 detach 现象，即被遮挡物与遮挡物相接的地方出现漏光。

## Implementation

Shadow mapping 是 2-pass，下面是两个 pass 的配置：

### 1. Light Pass 

- depth-only pass：offscreen 阶段，只有 vertex shader，没有 fragment shader。Light pass 为了生成 shadow map，depth buffer 在 fragment shader 之前生成，所以不需要 fragment shader。

- depth buffer(shadow map) ：分辨率 (2048x2048)，格式 ```VK_FORMAT_D32_SFLOAT```（需要
  ```vkGetPhysicalDeviceFormatProperties``` 查询是否支持，可能不同机器支持不同格式）。

- render target：只有 depth buffer，其 load op 设置为 ```VK_ATTACHMENT_LOAD_OP_CLEAR```，store op 设置为
  ```VK_ATTACHMENT_STORE_OP_STORE```。

- viewport 与 scissor 都设置为 depth buffer 的分辨率大小 (2048x2048)。

- Light Space MVP 变换：model 变换与 camera pass 设置相同即可，view 变换和 projection 变换要确保涵盖 camera pass 看到的所有几何，否则会有顶点的 depth 信息缺失，camera pass 中采样 shadow map 会出现阴影不正常。Projection 变换矩阵要对 $y$ 坐标进行反号，因为 Vulkan 坐标系是右手系，与 OpenGL 相反。

- Vertex Shader：light space 的 MVP 变换矩阵作为 uniform buffer 传递给 vertex shader

  ```glsl
  #version 450
  
  layout(location = 0) in vec3 aVertexPosition;
  layout(location = 1) in vec3 aVertexNormal;	// unused attribute
  layout(location = 2) in vec2 aTextureCoord;	// unused attribute
  layout(location = 3) in int aMaterialId;	// unused attribute
  
  layout(binding = 0) uniform uLightUbo{
      mat4 uLightMVP;	// light space MVP transformation
  };
  
  void main()
  {
      gl_Position = uLightMVP * vec4(aVertexPosition, 1.0);
  }
  ```


### 2. Camera Pass

- Depth and color Pass：绘制阶段，使用 blinn-phong 着色模型。

- render target：分辨率(1280x720)，color render target 是通过 swapchain 获取得到，其 load op 设置为 ```VK_ATTACHMENT_LOAD_OP_CLEAR```，store op 都设置为 ```VK_ATTACHMENT_STORE_OP_STORE```；depth buffer 需要自行创建，格式与 Light pass 中的 depth buffer 一致，不一样的是 store op 设置为
  ```VK_ATTACHMENT_STORE_OP_DONT_CARE```。

- viewport 与 scissor 都设置为 color render target 的分辨率大小(1280x720)。

- Camera space MVP 变换：同样，Projection 变换矩阵要对 $y$ 坐标进行反号处理。

- Vertex Shader：light space 与 camera 的 MVP 变换矩阵作为 uniform buffer 传递给 vertex shader

  ```glsl
  #version 450
  
  layout(binding = 0) uniform WorldMVP {
      mat4 worldMVP;	// camera space MVP
  };
  
  layout(binding = 1) uniform LightUbo {
      mat4 uLightMVP;	// light space MVP
  };
  
  layout(location = 0) in vec3 aVertexPosition;
  layout(location = 1) in vec3 aVertexNormal;
  layout(location = 2) in vec2 aTextureCoord;
  layout(location = 3) in int aMaterialId;	// unused attribute
  
  layout(location = 0) out vec2 vTextureCoord;
  layout(location = 1) out vec3 vFragPos;				// the world position of current vertex
  layout(location = 2) out vec3 vNormal;
  layout(location = 3) out vec4 vPositionFromLight;	// the position transformed into light space
  
  // (-1, 1) into (0, 1): 0.5xy+0.5w, for transfomation from clip space into NDC, need to be divided by w
  const mat4 biasMat = mat4( 
  	0.5, 0.0, 0.0, 0.0,
  	0.0, 0.5, 0.0, 0.0,
  	0.0, 0.0, 1.0, 0.0,
  	0.5, 0.5, 0.0, 1.0 );
  
  void main()
  {
      vFragPos = aVertexPosition;
      vNormal = aVertexNormal;
      gl_Position = worldMVP * vec4(aVertexPosition, 1.0);
      vPositionFromLight = biasMat * uLightMVP * vec4(aVertexPosition, 1.0);
      vTextureCoord = aTextureCoord;
  }
  ```

- Fragment Shader：模型贴图作为 diffuse texture，其 sampler(```uDiffuseTexSampler```) 设置 magFilter 和 minFilter 设置为 ```VK_FILTER_LINEAR``` ，mipmap 等级为 1，address mode 为 ```VK_SAMPLER_ADDRESS_MODE_REPEAT```。light pass 生成的 shadow map 贴图的 sampler 为```uShadowMapTexSampler```，设置与 diffuse texture 相同。

  ```glsl
  #version 450
  
  layout(push_constant) uniform uPushConstant {
      vec4 uLightPos;
      vec4 uCameraPos;
  };
  
  // material parameters
  layout(binding = 2) uniform uMaterial {
      vec4 uKd;	// diffuse coefficient
      vec4 uKs;	// specular coefficient
      float uLightIntensity;	// light intensity
  };
  
  layout(binding = 3) uniform sampler2D uDiffuseTexSampler;
  layout(binding = 4) uniform sampler2D uShadowMapTexSampler;
  
  layout(location = 0) in vec2 vTextureCoord;			// from vertex shader
  layout(location = 1) in vec3 vFragPos;				// from vertex shader
  layout(location = 2) in vec3 vNormal;				// from vertex shader
  layout(location = 3) in vec4 vPositionFromLight;	// from vertex shader
  
  layout(location = 0) out vec4 outColor;	// the output pixel color of fragment shader
  
  // calcute BlinnPhong shading equation
  vec3 BlinnPhong()
  {
      vec3 diffuseColor = vec3(1.0);
      diffuseColor = texture(uDiffuseTexSampler, vTextureCoord).rgb;
      
      vec3 ambient = 0.05 * diffuseColor;
      
      vec3 lightVec = uLightPos.xyz-vFragPos;
      vec3 lightDir = normalize(lightVec);
      vec3 normal = normalize(vNormal);
      float lightAttenCoff = uLightIntensity/dot(lightVec, lightVec);
  
      vec3 diffuse = max(dot(lightDir, normal), 0.0) * lightAttenCoff * diffuseColor;
      
      vec3 viewDir = normalize(uCameraPos.xyz-vFragPos);
      vec3 halfVec = normalize(lightDir+viewDir);
      float spec = pow(max(dot(viewDir, halfVec), 0.0), 35.0);
      vec3 specular = uKs.xyz * lightAttenCoff * spec;
      return ambient + diffuse + specular; 
  }
  
  void main()
  {
      // vPositionFromLight is the position 
      // which can be seen by camera (pass the depth-test) and is transformed into light space
      vec4 shadowCoord = vPositionFromLight / vPositionFromLight.w;
      float depthInLightSpace = shadowCoord.z;
      float visibility = 0.0;
      float depthFromShadowMap = texture(uShadowMapTexSampler, shadowCoord.xy).r;
      
      if(depthFromShadowMap >= depthInLightSpace)
      {
          visibility = 1.0;
      }
      outColor = vec4(visibility * BlinnPhong(), 1.0);
  }
  ```

### 3. Rendering Result

<a name="self-occlusion image"></a>

<img src="1. Basic Shadow Mapping.assets/image-20211016194244063.png" alt="image-20211016194244063" style="zoom:50%;" />

<center>自遮挡示例</center>

可以看出自遮挡问题很严重，下面就要使用 bias 方法消除自遮挡现象。bias 可以在 fragment shader 里添加，如从 shadow map 中采样得到的深度值加上一定的 bias，但 Vulkan 提供了更好的设置方式。 

### 4. 消除自遮挡：depth bias 设置

在 Vulkan pipeline 创建过程中，需要配置 [```VkPipelineRasterizationStateCreateInfo```](https://www.khronos.org/registry/vulkan/specs/1.2-extensions/man/html/VkPipelineRasterizationStateCreateInfo.html)，其中有几个 depth bias 相关成员：

- 其中本例未使用 ```depthClampEnable``` 与 ```depthBiasClamp```：分别设置为 false 与 0。
- 其中本例使用到的有：```depthBiasEnable```，```depthBiasConstantFactor```，```depthBiasSlopeFactor```：设置 ```depthBiasEnable``` 为 true，表示开启 depth bias 功能。```depthBiasConstantFactor``` 与
   ```depthBiasSlopeFactor``` 为两个 bias 参数，本例都设置为 2.5。

先看结果如何：

<img src="1. Basic Shadow Mapping.assets/image-20211016201102480.png" alt="image-20211016201102480" style="zoom:50%;" />

<center>应用 depth bias 的结果</center>

相比于 [未应用 depth bias](#self-occlusion image)，结果干净了许多。

```depthBiasConstantFactor``` 与 ```depthBiasSlopeFactor``` 的原理简介(详情请查看[depthbias-computation](https://www.khronos.org/registry/vulkan/specs/1.2-extensions/html/vkspec.html#primsrast-depthbias-computation))：

- 开启 depth bias 功能，Vulkan 管线会计算 shadow map 中 depth 相对于采样坐标的导数，```depthBiasSlopeFactor``` 则是与这个导数相乘得到关于斜率的 bias。这种做法有一个自适应 bias 的功能，即 depth 相对于采样坐标变化较快的地方，bias 会相对较大，反之相对较小。假设当前 shadow map 的采样点的 depth 斜率为 $m$，那么 ```depthBiasSlopeFactor``` 引入的 bias = ```depthBiasSlopeFactor``` * $m$。
- ```depthBiasConstantFactor``` 主要是为了应对浮点计算引入的误差。浮点计算会由于舍入而引入误差，舍入只会发生在尾数的最后一位，那么对于已知的浮点数，可以确定一次舍入所引入的误差范围，即尾数的末位的精度（因为舍入要么末位加1，要么末位减1）。假设末位精度为 $r$（shadow map 生成时，depth 已经确定，因此末位精度也已经确定），```depthBiasConstantFactor```引入的 bias = ```depthBiasConstantFactor``` * $r$。 
- 最终的 depth bias 为 ```depthBiasConstantFactor``` 与 ```depthBiasSlopeFactor``` 引入的 bias 之和。如果 ```depthClampEnable``` 启用，depth bias 在应用到 shadow map之前，还会再经过一次 clamp，clamp 参数为 [0, ```depthBiasClamp```]。



























